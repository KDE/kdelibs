# Original Author was Kalle@kde.org
# I lifted it in some mater. (Stephan Kulow)
# I used much code from Janos Farkas

dnl Process this file with autoconf to produce a configure script.
AC_INIT(acinclude.m4) dnl a source file from your sub dir

dnl This is so we can use kde-common
AC_CONFIG_AUX_DIR(admin)

AC_CANONICAL_SYSTEM 
AC_ARG_PROGRAM

dnl Automake doc recommends to do this only here. (Janos)
AM_INIT_AUTOMAKE(kdelibs, 1.1) dnl searches for some needed programs
AC_PROG_INSTALL

dnl make $KDEDIR the default for the installation
AC_PREFIX_DEFAULT(${KDEDIR:-/usr/local/kde})

dnl generate the config header
AM_CONFIG_HEADER(config.h) dnl at the distribution this done

dnl Checks for programs.
AC_CHECK_COMPILERS
AM_ENABLE_SHARED(yes)
AM_ENABLE_STATIC(no)
KDE_PROG_LIBTOOL

dnl find out some variables (not supported yet)
AC_AIX
AC_MINIX

dnl for NLS support. Call them in this order!
dnl WITH_NLS is for the po files, GNU_GETTEXT for the sources
AC_LANG_CPLUSPLUS
AM_KDE_WITH_NLS
AM_KDE_GNU_GETTEXT  

dnl Checks for header files.
AC_LANG_CPLUSPLUS
KDE_CHECK_STL
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h sys/cdefs.h fnmatch.h sysent.h strings.h sys/stat.h sys/select.h sys/socket.h linux/socket.h socketbits.h sigaction.h paths.h)

dnl Checks for libraries.
AC_BASE_PATH_KDE([don't test]) dnl kdelibs is a special case
AC_CREATE_KFSSTND(default)
AC_SUBST_KFSSTND
AC_CHECK_LIB(compat, main, [LIBCOMPAT="-lcompat"]) dnl for FreeBSD

dnl Image libraries
KDE_CREATE_LIBS_ALIASES
KDE_CHECK_KIMGIO

# configure would do this very late. Too late for us!
test "x$prefix" = xNONE && prefix=$ac_default_prefix

AC_DEFINE_UNQUOTED(KDEDIR, "$prefix")

if test "x$exec_prefix" = xNONE; then
  ac_kde_exec_prefix=KDEDIR
else 
  ac_kde_exec_prefix=$exec_prefix
fi

AC_DEFUN(AC_DEFINE_PATH,
[
  eval "tempdir=`echo $2 | sed -e 's/\$(exec_prefix)/$ac_kde_exec_prefix/' -e 's/\$(prefix)/KDEDIR/'`"
  AC_DEFINE_UNQUOTED($1, "$tempdir")
])

# Let make expand exec_prefix.
test "x$exec_prefix" = xNONE && exec_prefix='$(prefix)' 

AC_DEFINE_PATH(KDE_HTMLDIR, $kde_htmldir)
AC_DEFINE_PATH(KDE_APPSDIR, $kde_appsdir)
AC_DEFINE_PATH(KDE_ICONDIR, $kde_icondir)
AC_DEFINE_PATH(KDE_SOUNDDIR, $kde_sounddir)
AC_DEFINE_PATH(KDE_DATADIR, $kde_datadir)
AC_DEFINE_PATH(KDE_LOCALE, $kde_locale)
AC_DEFINE_PATH(KDE_CGIDIR, $kde_cgidir)
AC_DEFINE_PATH(KDE_CONFIGDIR, $kde_confdir)
AC_DEFINE_PATH(KDE_MIMEDIR, $kde_mimedir)
AC_DEFINE_PATH(KDE_TOOLBARDIR, $kde_toolbardir)
AC_DEFINE_PATH(KDE_WALLPAPERDIR, $kde_wallpaperdir)
AC_DEFINE_PATH(KDE_BINDIR, $kde_bindir)
AC_DEFINE_PATH(KDE_PARTSDIR, $kde_partsdir)

ac_cpp_safe=$ac_cpp
ac_cpp='$CXXCPP $CPPFLAGS $X_INCLUDES'
AC_CHECK_HEADERS(X11/extensions/shape.h)
ac_cpp=$ac_cpp_safe

dnl Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_TIME

dnl check if the compiler has bool
AC_CHECK_BOOL
dnl AC_CHECK_GNU_EXTENSIONS

dnl Checks for library functions. 
AC_CHECK_FUNCS(socket vsnprintf seteuid setegid random)
AC_CHECK_USLEEP
AC_CHECK_SETENV
AC_CHECK_GETDOMAINNAME
AC_CHECK_GETHOSTNAME
AC_CHECK_RANDOM
AC_CHECK_KSIZE_T

if test -n "$qt_includes"; then
  QKEYCODE_H="$qt_includes/qkeycode.h"
fi
AC_SUBST(QKEYCODE_H)

dnl Perform program name transformation
AC_ARG_PROGRAM

dnl output files
topdir=`pwd`
AC_SUBST(x_includes)
AC_SUBST(x_libraries)
AC_SUBST(topdir)
AC_SUBST(LIBSOCKET)
AC_SUBST(LIBCOMPAT)

dnl CXXFLAGS="$CXXFLAGS -ansi -pedantic"
KDE_CHECK_ANSI

AC_ARG_ENABLE(new-stuff, [  --enable-new-stuff      compile the experimental code too (none currently)], [
    if test "$enableval" = "no";
      then ac_new_stuff="no"
    else ac_new_stuff="yes"
  fi
  ], [ac_new_stuff="no"]
  )
  
if test "$ac_new_stuff" = "yes"; then
  EXTRA_SUBDIRS="" dnl put here experimental stuff, if any
else
  EXTRA_SUBDIRS=""
fi

AC_SUBST(EXTRA_SUBDIRS)

AC_OUTPUT(Makefile \
	kio/Makefile \
	kdecore/Makefile \
	kdeui/Makefile \
	kimgio/Makefile \
	khtmlw/Makefile \
	khtml/Makefile \
	kdetest/Makefile \
	kfile/Makefile \
	kdeutils/Makefile \
	kio/Makefile \
	jscript/Makefile \
	kfmlib/Makefile \
	mediatool/Makefile \
	kspell/Makefile \
	kab/Makefile \
	po/Makefile \
	toolbar/Makefile \
	doc/Makefile \
	doc/kfile/Makefile \
	doc/kspell/Makefile )

