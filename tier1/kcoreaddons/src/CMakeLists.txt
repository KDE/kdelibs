# Configure checks for the caching subdir
include(CheckIncludeFiles)
check_include_files("sys/types.h;sys/mman.h" HAVE_SYS_MMAN_H)
configure_file(caching/config-caching.h.cmake ${CMAKE_CURRENT_BINARY_DIR}/config-caching.h)

set(kcoreaddons_OPTIONAL_SRCS )
set(kcoreaddons_OPTIONAL_LIBS )

if (FAM_FOUND)
   include_directories(${FAM_INCLUDE_DIR})
   set(kcoreaddons_OPTIONAL_LIBS ${kcoreaddons_OPTIONAL_LIBS} ${FAM_LIBRARIES})
endif ()

if(NOT WIN32)
    set(kcoreaddons_OPTIONAL_SRCS caching/kshareddatacache.cpp)
    set(kcoreaddons_OPTIONAL_LIBS ${kcoreaddons_OPTIONAL_LIBS} ${CMAKE_THREAD_LIBS_INIT})

    set_source_files_properties(caching/kshareddatacache.cpp
        PROPERTIES COMPILE_FLAGS -fexceptions)

    # QT5_BUILD Qt 5 Porting: Find out what to do about this:
#     set_source_files_properties(util/kshareddatacache.cpp
#         PROPERTIES COMPILE_FLAGS ${KDE4_ENABLE_EXCEPTIONS})
else(NOT WIN32)
    set(kcoreaddons_OPTIONAL_SRCS
        caching/kshareddatacache_win.cpp
        io/kdirwatch_win.cpp
    )
endif(NOT WIN32)

if (WIN32)
    set(kcoreaddons_OPTIONAL_SRCS
        ${kcoreaddons_OPTIONAL_SRCS}
        io/klockfile_win.cpp
        text/kmacroexpander_win.cpp
    )
endif ()

if (UNIX)
    set(kcoreaddons_OPTIONAL_SRCS
        ${kcoreaddons_OPTIONAL_SRCS}
        io/klockfile_unix.cpp
        text/kmacroexpander_unix.cpp
    )
endif ()

set(libkcoreaddons_SRCS
    io/kautosavefile.cpp
    io/kdirwatch.cpp
    io/kfilesystemtype_p.cpp
    io/kbackup.cpp
    io/kurlmimedata.cpp
    jobs/kcompositejob.cpp
    jobs/kjob.cpp
    jobs/kjobtrackerinterface.cpp
    jobs/kjobuidelegate.cpp
    randomness/krandom.cpp
    randomness/krandomsequence.cpp
    text/kmacroexpander.cpp
    text/kstringhandler.cpp
    ${kcoreaddons_OPTIONAL_SRCS}
)


include_directories(
    ${CMAKE_CURRENT_BINARY_DIR}/io/
    ${CMAKE_CURRENT_SOURCE_DIR}/io/
    ${CMAKE_CURRENT_SOURCE_DIR}/jobs/
    ${CMAKE_CURRENT_SOURCE_DIR}/randomness/
    ${CMAKE_CURRENT_SOURCE_DIR}/kernel/
    ${CMAKE_CURRENT_SOURCE_DIR}/text/
)

include_directories(${QT_MKSPECS_DIR}/default) # for qplatformdefs.h

add_library(kcoreaddons ${libkcoreaddons_SRCS})
generate_export_header(kcoreaddons)

target_link_libraries(kcoreaddons
  LINK_PUBLIC ${QT_QTCORE_LIBRARY} ${kcoreaddons_OPTIONAL_LIBS} ${inqt5_LIBRARY} ${kdeqt5staging_LIBRARY}
)
if(NOT WIN32)
    target_link_libraries(kcoreaddons LINK_PRIVATE pthread)
endif(NOT WIN32)

set_target_properties(kcoreaddons PROPERTIES VERSION   ${KCOREADDONS_VERSION_STRING}
                                             SOVERSION ${KCOREADDONS_SOVERSION}
)

install(TARGETS kcoreaddons EXPORT kcoreaddonsTargets ${INSTALL_TARGETS_DEFAULT_ARGS})

install(FILES
    caching/kshareddatacache.h
    io/kautosavefile.h
    io/kdirwatch.h
    io/klockfile.h
    io/kbackup.h
    io/kurlmimedata.h
    jobs/kcompositejob.h
    jobs/kcompositejob_p.h
    jobs/kjob.h
    jobs/kjob_p.h
    jobs/kjobtrackerinterface.h
    jobs/kjobuidelegate.h
    # Only so that we can build kdepimlibs against kf5.
    kernel/qlocalizedstring_porting.h
    randomness/krandom.h
    randomness/krandomsequence.h
    text/kmacroexpander.h
    text/kstringhandler.h
    ${CMAKE_CURRENT_BINARY_DIR}/kcoreaddons_export.h
    DESTINATION ${INCLUDE_INSTALL_DIR} COMPONENT Devel
)
